buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:5.1.0.4882'
        classpath 'com.github.spotbugs.snom:spotbugs-gradle-plugin:5.2.0'
    }
}

plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.github.ben-manes.versions' version '0.51.0'
}

repositories {
    mavenCentral()
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'java-library'
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply from: 'gradle/checkstyle.gradle'
apply plugin: 'org.sonarqube'
apply plugin: 'com.github.spotbugs'

group = 'com.example'
version = System.getenv('BUILD_ID') ? System.getenv('BUILD_ID') : '1.0.0-SNAPSHOT'
description = 'flight-booking-system'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    integrationTestCompile.extendsFrom testCompile
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-logging'

    implementation 'org.postgresql:postgresql:42.7.1'
    implementation 'org.liquibase:liquibase-core:4.25.0'
    implementation 'redis.clients:jedis:5.1.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'

    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'com.h2database:h2:2.2.224'
    testImplementation 'org.testcontainers:testcontainers:1.19.3'
    testImplementation 'org.testcontainers:postgresql:1.19.3'
    testImplementation 'org.testcontainers:junit-jupiter:1.19.3'
    testImplementation 'org.mockito:mockito-core:5.8.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.8.0'
    testImplementation 'org.assertj:assertj-core:3.25.1'
    testImplementation 'com.github.tomakehurst:wiremock-jre8:2.35.0'
}

jacoco {
    toolVersion = "0.8.11"
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
        java.srcDirs = ['src/integrationTest/java']
        resources.srcDirs = ['src/integrationTest/resources']
    }
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.destination file("$buildDir/reports/jacoco")
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/config/**',
                    '**/dto/**',
                    '**/model/**',
                    '**/*Application.class'
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [''])
        }))
    }
    violationRules {
        rule {
            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
        }
    }
}

sonar {
    properties {
        property "sonar.projectKey", "vuthainguyen1602_flight-booking-system-online"
        property "sonar.organization", "vuthainguyen1602"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.qualitygate.wait", "true"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "$buildDir/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.exclusions", "**/config/**,**/dto/**,**/model/**,**/*Application.java"
        property "sonar.java.binaries", "$buildDir/classes/java/main,$buildDir/classes/java/test,$buildDir/classes/java/integrationTest"
    }
}

spotbugs {
    ignoreFailures = false
    showStackTraces = true
    showProgress = true
    effort = 'default'
    reportLevel = 'medium'
    visitors = ['FindSqlInjection', 'SwitchFallthrough']
    omitVisitors = ['FindNonShortCircuit']
    reportsDir = file("$buildDir/spotbugs")
    excludeFilter = file("gradle/config/spotbugs-exclude.xml")
    maxHeapSize = '1g'
}

spotbugsMain {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

bootJar {
    enabled = true
    archiveFileName = "${project.name}-${project.version}.jar"
}

jar {
    enabled = false
    manifest {
        attributes 'Main-Class': 'com.example.flightbookingsystem.FlightBookingSystemApplication'
    }
}

publishing {
    publications {
        bootJava(MavenPublication) {
            artifact bootJar
        }
    }

    repositories {
        maven {
            url = uri("$buildDir/repos")
        }
    }
}

task copyDependencies(type: Copy) {
    from(configurations.runtimeClasspath)
    into("${buildDir}/libs/dependencies")
}

task cleanAll {
    group = "build"
    description = "Cleans build directories and dependencies."
    doLast {
        delete("${buildDir}")
    }
}

task setExecutable {
    doLast {
        def isWindows = System.getProperty('os.name').toLowerCase().contains('windows')
        def gradlewScript = isWindows ? 'gradlew.bat' : './gradlew'

        if (!isWindows) {
            project.exec {
                commandLine 'chmod', '+x', gradlewScript
            }
        }
    }
}
